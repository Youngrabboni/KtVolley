plugins {
    id "com.android.library"
    id "kotlin-android"
    id "com.jfrog.bintray" version "1.8.4"
    id "maven-publish"
}

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 28
        versionName "0.4"
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.1.1'

    implementation 'com.android.volley:volley:1.1.1'

    implementation 'com.fasterxml.jackson.core:jackson-core:2.9.8'

    implementation 'com.fasterxml.jackson.core:jackson-databind:2.9.5'
    implementation 'com.bluelinelabs:logansquare:1.3.7'

    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation 'androidx.fragment:fragment-ktx:1.0.0'
}

project.afterEvaluate {
    publishing {
        publications {
            aar(MavenPublication) {
                groupId 'com.diamondedge'
                artifactId 'ktvolley'
                version = android.defaultConfig.versionName

                artifact("$buildDir/outputs/aar/${project.getName()}-release.aar")
                artifact androidSourcesJar
                pom.withXml {
                    def dependencies = asNode().appendNode('dependencies')
                    configurations.api.allDependencies.each {
                        def dependency = dependencies.appendNode('dependency')
                        dependency.appendNode('groupId', it.group)
                        dependency.appendNode('artifactId', it.name)
                        dependency.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

bintray {
    user = bintray_user
    key = bintray_key
    publications = ['aar']

    pkg {
        repo = 'android'
        name = 'KtVolley'
        userOrg = 'diamondedge'
        publicDownloadNumbers = true
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/ellsworthrw/KtVolley'
        version {
            name = android.defaultConfig.versionName
            desc = "KtVolley ${android.defaultConfig.versionName}"
            vcsTag = "v${android.defaultConfig.versionName}"
            released = new Date()
        }
    }
    publish = true
}

task androidSourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
